#!/usr/bin/env spry

# A sample Spry application that is a trivial file editor.

# First we need to initialize libui
uiInit

# Variable holding file contents
content = ""

# Variable holding last known filepath
path = nil

# Two funcs for opening and saving a file
openContent = func [
  # this is a language wart I am fixing, we should not need ".."
  ..path = (win openFile)
  path nil? else: [
    content = readFile path
    contentEntry text: content
  ]
]
saveContent = func [:path
  content = (contentEntry text)
  writeFile path content
]

# A handler for closing
closeHandler = [
  win message: "Bye bye" title: "Braindead editor"
  controlDestroy win
  uiQuit
]

# Build the app menu
menu = newMenu "File"
menu onShouldQuit: closeHandler

item = (menu menuAppendItem: "Open ...")
item onMenuItemClicked: [openContent]

item = (menu menuAppendItem: "Save as ...")
item onMenuItemClicked: [
  ..path = (win saveFile)
  path nil? else: [
    saveContent path
  ]
]

item = (menu menuAppendQuitItem)

helpMenu = newMenu "Help"
item = (helpMenu menuAppendItem: "Help")
item onMenuItemClicked: [
  win message: "Sorry..." title: "No help to get"
]

# Silly About
item = (helpMenu menuAppendAboutItem)
item onMenuItemClicked: [
  win message: "This is a truly trivial file editor." title: "About Braindead editor"
]

# Create a new app Window
win = newWindow "Braindead editor" 640 400 true
win windowMargin: 1

# Create a multiline text entry field for content
contentEntry = newMultilineEntryText

# Some boxes put stuff in
layout = newVerticalBox
buttons = newHorizontalBox

# Some buttons and their handlers
saveit = newButton "Save"
saveit onClicked: [
  path nil? then: [
    ..path = (win saveFile)
  ]
  path nil? else: [
    saveContent path
  ]
]
clearit = newButton "Clear"
clearit onClicked: [contentEntry text: ""]
quitit = newButton "Quit"
quitit onClicked: closeHandler

# Put things in the boxes
layout padding: 1
layout append: contentEntry stretch: 1
layout append: buttons stretch: 0
buttons append: saveit stretch: 0
buttons append: clearit stretch: 0
buttons append: quitit stretch: 0

# Add box to window
win windowSetChild: layout

# Set initial text in text component
contentEntry text: content

# Set a handler on closing window
win onClosing: closeHandler

# Show the window
win show

# Enter libui's event loop
win uiMain